namespace Songhay.Modules.Bolero.Visuals.Bulma

open Bolero
open Bolero.Html

open Songhay.Modules.Bolero.Models
open Songhay.Modules.Bolero.SvgUtility

///<summary>
/// Bulma Elements
/// â€œEssential interface elements that only require a single CSS classâ€¦â€
/// â€” https://bulma.io/documentation/elements/
///</summary>
module Element =
    /// <summary>
    /// â€œA single class to handle WYSIWYG generated content, where only HTML tags are availableâ€¦â€
    /// </summary>
    /// <remarks>
    /// ğŸ“– https://bulma.io/documentation/elements/content/
    /// </remarks>
    let bulmaContent (moreClasses: CssClassesOrEmpty) (childNode: Node) =
        div {
            CssClasses [ CssClass.content ] |> moreClasses.ToHtmlClassAttribute

            childNode
        }

    /// <summary>
    /// â€œâ€¦a container for any type of icon fontâ€¦â€
    /// </summary>
    /// <remarks>
    /// ğŸ“– https://bulma.io/documentation/elements/icon/
    /// </remarks>
    let bulmaIcon (visualNode: Node) = span { "icon" |> CssClasses.toHtmlClass; AriaHidden.ToAttrWithTrueValue; visualNode }

    /// <summary>
    /// Calls <see cref="svgViewBox" /> with the specified <see cref="BulmaSquareDimension" />.
    /// </summary>
    let bulmaIconSvgViewBox (square: BulmaSquareDimension) =
        svgViewBox (0,0) (square.ToWidthOrHeight, square.ToWidthOrHeight)

    /// <summary>
    /// â€œYou can combine an icon with text, using the icon-text wrapperâ€¦â€
    /// </summary>
    /// <remarks>
    /// ğŸ“– https://bulma.io/documentation/elements/icon/#icon-text
    /// </remarks>
    let bulmaIconText (moreClasses: CssClassesOrEmpty) (attr: HtmlAttributeOrEmpty) (childNode: Node) =
        span {
            CssClasses [ "icon-text" ] |> moreClasses.ToHtmlClassAttribute
            attr.Value
            span { childNode }
        }

    /// <summary>
    /// â€œA container for responsive imagesâ€¦â€
    /// </summary>
    /// <remarks>
    /// ğŸ“– https://bulma.io/documentation/elements/image/
    /// </remarks>
    let bulmaImageContainer (size: BulmaRatioDimension) (attributes: HtmlAttributeOrEmpty) (visualNode: Node) =
        figure {
            size |> CssClass.imageContainer |> CssClasses.toHtmlClassFromList
            attributes.Value

            visualNode
        }

    /// <summary>
    /// â€œBold notification blocks, to alert your users of somethingâ€¦â€
    /// </summary>
    /// <remarks>
    /// ğŸ“– https://bulma.io/documentation/elements/notification/
    /// </remarks>
    let bulmaNotification (moreClasses: CssClassesOrEmpty) (childNode: Node) =
        div {
             CssClasses [ CssClass.notification ] |> moreClasses.ToHtmlClassAttribute

             childNode
        }
